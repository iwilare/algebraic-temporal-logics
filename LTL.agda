{-# OPTIONS --sized-types #-}

open import SortedAlgebra using (Signature)

module LTL {РёЊ} {╬Б : Signature {РёЊ}} where

open import Data.Vec using (_Рѕи_)

open Signature ╬Б
open SortedAlgebra.Term ╬Б

data LTL {n} (╬Њ : Ctx n) : Set РёЊ where
  true  : LTL ╬Њ
  false : LTL ╬Њ
  !_    : LTL ╬Њ Рєњ LTL ╬Њ
  _РѕД_   : LTL ╬Њ Рєњ LTL ╬Њ Рєњ LTL ╬Њ
  _Рѕе_   : LTL ╬Њ Рєњ LTL ╬Њ Рєњ LTL ╬Њ
  O_    : LTL ╬Њ Рєњ LTL ╬Њ
  A_    : LTL ╬Њ Рєњ LTL ╬Њ
  _F_   : LTL ╬Њ Рєњ LTL ╬Њ Рєњ LTL ╬Њ
  _U_   : LTL ╬Њ Рєњ LTL ╬Њ Рєњ LTL ╬Њ
  _W_   : LTL ╬Њ Рєњ LTL ╬Њ Рєњ LTL ╬Њ
  _T_   : LTL ╬Њ Рєњ LTL ╬Њ Рєњ LTL ╬Њ
  РѕЃ<_>_ : (¤ё : ­ЮЊб)
        Рєњ LTL (¤ё Рѕи ╬Њ)
        Рєњ LTL ╬Њ
  Рѕђ<_>_ : (¤ё : ­ЮЊб)
        Рєњ LTL (¤ё Рѕи ╬Њ)
        Рєњ LTL ╬Њ
  _РЅАрхЌ_  : Рѕђ {i ¤ё}
        Рєњ ╬Њ Ріб ¤ё РЪе i РЪЕ
        Рєњ ╬Њ Ріб ¤ё РЪе i РЪЕ
        Рєњ LTL ╬Њ
  _РЅбрхЌ_  : Рѕђ {i ¤ё}
        Рєњ ╬Њ Ріб ¤ё РЪе i РЪЕ
        Рєњ ╬Њ Ріб ¤ё РЪе i РЪЕ
        Рєњ LTL ╬Њ

РЌЄ_ : Рѕђ {n} {╬Њ : Ctx n} Рєњ LTL ╬Њ Рєњ LTL ╬Њ
РЌЄ ¤Ћ = true U ¤Ћ

РќА_ : Рѕђ {n} {╬Њ : Ctx n} Рєњ LTL ╬Њ Рєњ LTL ╬Њ
РќА ¤Ћ = ¤Ћ W false

РЌЄ*_ : Рѕђ {n} {╬Њ : Ctx n} Рєњ LTL ╬Њ Рєњ LTL ╬Њ
РЌЄ* ¤Ћ = true F ¤Ћ

РќА*_ : Рѕђ {n} {╬Њ : Ctx n} Рєњ LTL ╬Њ Рєњ LTL ╬Њ
РќА* ¤Ћ = ¤Ћ T false

infix 15 _РѕД_ _Рѕе_
infix 20 РЌЄ_ РќА_ РЌЄ*_ РќА*_ O_ A_ _U_ _F_ _W_ _T_
infix 23 РѕЃ<_>_ Рѕђ<_>_
infix 25 _РЅАрхЌ_ _РЅбрхЌ_
